plugins {
    id 'java'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'checkstyle'
}

checkstyle {
    toolVersion = '10.20.1'
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    ignoreFailures = false
    maxWarnings = 0
}

spotless {
    java {
        palantirJavaFormat()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

group = 'ro.stancalau.livekit'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:$lombok_version"
    testAnnotationProcessor "org.projectlombok:lombok:$lombok_version"

    implementation "org.projectlombok:lombok:$lombok_version"
    testImplementation "org.projectlombok:lombok:$lombok_version"

    implementation "org.testcontainers:selenium:$selenium_container_version"
    implementation "org.testcontainers:testcontainers:$test_containers_version"

    implementation "org.seleniumhq.selenium:selenium-java:$selenium_version"
    implementation "org.seleniumhq.selenium:selenium-chrome-driver:$selenium_version"
    implementation "org.seleniumhq.selenium:selenium-remote-driver:$selenium_version"

    testImplementation platform("org.junit:junit-bom:$junit_version")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation "org.junit.platform:junit-platform-suite:$junit_platform_suite_version"

    testImplementation "io.cucumber:cucumber-java:$cucumber_version"
    testImplementation "io.cucumber:cucumber-junit-platform-engine:$cucumber_junit_platform_version"

    implementation "ch.qos.logback:logback-classic:$logback_version"

    implementation "io.livekit:livekit-server:$livekit_server_version"
    testImplementation "com.auth0:java-jwt:$auth0_jwt_version"

    implementation "org.mock-server:mockserver-client-java:$mockserver_client_version"

    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"

    implementation "org.awaitility:awaitility:$awaitility_version"

    implementation platform("software.amazon.awssdk:bom:$aws_sdk_version")
    implementation "software.amazon.awssdk:s3"
}

test {
    useJUnitPlatform()

    systemProperty 'livekit.version', project.findProperty('livekit_docker_version') ?: 'v1.8.4'

    if (project.hasProperty('livekit_docker_version')) {
        systemProperty 'livekit.version', project.property('livekit_docker_version')
    }

    systemProperty 'livekit.js.version', project.findProperty('livekit_js_version') ?: '2.6.4'
    systemProperty 'ingress.version', project.findProperty('ingress_docker_version') ?: 'v1.4.3'

    systemProperty "cucumber.filter.tags", System.getProperty("cucumber.filter.tags")
    systemProperty "cucumber.filter.name", System.getProperty("cucumber.filter.name")
}

task bddTest(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    include '**/RunCucumberTests.class'

    maxParallelForks = 1

    def parallelism = System.getProperty('parallelism', '1')

    systemProperty 'cucumber.execution.parallel.enabled', 'true'

    systemProperty 'cucumber.execution.timeout.default', '600000'
    systemProperty 'cucumber.execution.parallel.config.strategy', 'fixed'
    systemProperty 'cucumber.execution.parallel.config.fixed.parallelism', parallelism
    systemProperty 'cucumber.execution.parallel.config.fixed.max-pool-size', parallelism

    systemProperty 'junit.jupiter.execution.parallel.enabled', 'true'
    systemProperty 'junit.jupiter.execution.parallel.mode.default', 'concurrent'
    systemProperty 'junit.jupiter.execution.parallel.config.strategy', 'fixed'
    systemProperty 'junit.jupiter.execution.parallel.config.fixed.parallelism', parallelism
}

build.dependsOn bddTest

clean {
    delete 'out'
}
